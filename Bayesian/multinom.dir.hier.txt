
    model {
    for (i in 1:N) {
    y [i, 1:J] ~ dmulti(pi[i, 1:J], n[i])   # multinomial distribution of response vectors
    n[i] <- sum(y[i, ]) # calculate sample size per individual
    pi[i, 1:J] ~ ddirch(alpha[]) # individual preferences are distributed Dirichlet(alpha)
    }
    for (j in 1:J){
    alpha[j] <- q[j] * w # estimate the parameters that make up alpha
    }
    q[1:J]  ~ ddirch(u[1:J]) # prior for q
    for (j in 1:J){
    u[j]  <- 1
    }
    w ~ dunif(0, 30) # prior for w
    for (i in 1:N){
    for(j in 1:J){
    diff.prop[i,j]  <- abs(pi[i, j] - q[j])
    }
    PS[i]  <- 1 - 0.5 * sum(diff.prop[i,1:J]) # calculate PSi
    }
    mean.PS  <- mean(PS[]) # calculate IS
    }
    
